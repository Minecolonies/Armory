// For those who want the bleeding edge
buildscript {
    repositories {
        jcenter()
        mavenLocal()
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            name = "m2"
            url = "https://plugins.gradle.org/m2/"
        }
        maven {
            name = "jitpack"
            url = "https://jitpack.io"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'com.github.jriwanek:cursegradle:master-SNAPSHOT'
    }
}

plugins {
    //id "com.matthewprenger.cursegradle" version "1.0.9"
}

repositories {
    maven {
        name 'LDTTeam Artifactory'
        url 'https://ldtteam.jfrog.io/ldtteam/modding'
    }
}

apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'
apply plugin: 'com.matthewprenger.cursegradle'

sourceCompatibility = 1.8
targetCompatibility = 1.8

//Buildscript: armory
// Load the properties of this project.
ext.configFile = file "build.properties"

//Reference the properties inside the project:
configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

//These configurations are needed for uploading to a MavenRepo:
configurations {
    deployerJars
}

//Initializing the mod environment
version = config.minecraft_version.toString() + "-" + System.getenv()["Version"]
group = "com.smithsmodding.armory"
archivesBaseName = "armory"

minecraft {
    version = config.minecraft_version + "-" + config.forge_version
    runDir = "run"

    mappings = "snapshot_20180421"

    useDepAts = true

    //Replacing stuff inside the code:
    replace "@VERSION@", project.version
    replace "@MCVERSION@", config.minecraft_version
    replace "@APIVERSION@", project.version
}

dependencies {
    //Mod dependencies
    deobfCompile('com.smithsmodding.smithscore:smithscore:1.12.2-0.1.+')

    //deobfCompile group: 'com.smithsmodding.smithscore', name: 'smithscore', version: config.minecraft_version + "-" + config.smithscore_version
    deobfCompile "mezz.jei:jei_" + config.minecraft_version + ":" + config.jei_version

    compile 'org.jetbrains:annotations:15.0'
}

sourceSets {
    main {
        java {
            srcDir 'src/main'
        }
        resources {
            srcDir 'src/resources'
        }
    }
    api {
        java {
            srcDir 'src/api'
        }
    }
    test {
        java {
            srcDir 'src/test/api'
            srcDir 'src/test/main'
        }
    }
}

// Add API dir to the IDEA module
idea.module.sourceDirs += sourceSets.api.java.srcDirs

//This will process all the resources used during build, and needed for running the project
processResources
        {
            //Replaces stuff in mcmod.info, nothing else
            from(sourceSets.main.resources.srcDirs) {
                include 'mcmod.info'

                // replace version and mcversion
                expand 'VERSION': project.version, 'MCVERSION': project.minecraft.version
            }

            //Copy everything else, thats not the mcmod.info
            from(sourceSets.main.resources.srcDirs) {
                exclude 'mcmod.info'
            }
            exclude '**/Thumbs.db'
        }

jar {
    manifest {
        attributes 'FMLAT': 'Armory_at.cfg'
    }

    from sourceSets.main.output
    from sourceSets.api.output
}

javadoc {
    source += sourceSets.api.allSource
}

// add a javadoc jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task apiJar(type: Jar) {
    from sourceSets.api.output
    classifier = 'api'
}


artifacts {
    archives javadocJar
    archives apiJar
}

reobf{
    jar{}
    apiJar{}
}

curseforge {
    if (System.getenv().CURSEAPIKEY != null && System.getenv().CURSERELEASETYPE != null)
    {
        apiKey = System.getenv().CURSEAPIKEY

        project {
            id = '231229'

            changelog = file("build/changelog.md")
            changelogType = "markdown"

            releaseType = System.getenv().CURSERELEASETYPE

            mainArtifact (jar) {
                displayName = "Armory: " + version

                relations {
                    requiredDependency 'smithscore' // SmithScore is required
                }
            }

            addArtifact javadocJar
            addArtifact apiJar
            addArtifact sourceJar
        }
    }
    else
    {
        logger.lifecycle("Cannot run the CurseUpload sequence. No API-Key or release type has been provided.")
    }
}

task("createChangelog") {
    group = 'upload'

    doLast {
        def teamCityURL = "https://teamcity.minecolonies.com/"
        def file = new FileOutputStream("build/changelog.md")
        def out = new BufferedOutputStream(file)

        println("Retrieving changes from: " + teamCityURL + "guestAuth/app/rest/changes?locator=build:(id:" + teamcity["teamcity.build.id"] + ")")
        def changesXML = new XmlSlurper().parse(teamCityURL + "guestAuth/app/rest/changes?locator=build:(id:" + teamcity["teamcity.build.id"] + ")")

        def changes = changesXML.change
        println("createChangelog: Identified " + changes.size() + " changes to be written into the changelog.")

        out << "# Armory Changelog \n"
        out << "## Version: _" + version + "_ \n"

        if (changes.size() > 0) {
            for (int i = 0; i < changes.size(); i++) {
                def changeDetailsURL = teamCityURL + "guestAuth/app/rest/changes/id:" + changes[i].@id.text()
                def changeDetailsXml = new XmlSlurper().parse(changeDetailsURL)
                def changeComment = changeDetailsXml.comment.text().trim().split("\n")[0]

                out << "* " + changeComment + "\n"
            }
        } else {
            out << "No Changes detected!"
        }

        out.close()
    }
}
